// -----------------------------------------------------------------------//
// Name: Paulo Radatz 
// e-mail: paulo.radatz@gmail.com 
// -----------------------------------------------------------------------//


// -----------------------------------------------------------------------//
// -----------------------------Circuito Basico----------------------//
// -----------------------------------------------------------------------//

Clear

// Equivalente de Thevenin
New Circuit.Fonte phases=3 basekv=0.220 bus1=A

// LineCode

New Linecode.Arranjo nphases=3 basefreq=60 units=km
~ Rmatrix=[0.19  |0.06  0.19  |0.06  0.06  0.19]		!ohm/km
~ Xmatrix=[0.86  |0.49  0.86  |0.44  0.49  0.86]          !ohm/km
~ Cmatrix=[9.34  |-2.21  9.93  |-0.99  -2.24  9.27]       !nF/km

New Line.Linha1 Phases=3 Bus1=A Bus2=B Length=0.2 units=km linecode=Arranjo
New Line.Linha2 Phases=3 Bus1=B Bus2=C Length=0.15 units=km linecode=Arranjo

New energymeter.medidor element=line.linha1 terminal=1


New Loadshape.Semana npts=24 interval=1 
~ mult=(0.69 0.50 0.44 0.41 0.55 0.85 1.01 0.80 0.89 0.91 1.02 1.03 1.11 0.98 0.94 0.94 1.02 1.26 1.51 1.59 1.75 1.50 1.29 0.89)

New Load.Carga phases=3 conn=wye Bus1=B kw=25 pf=0.92 kv=0.22 daily=Semana

New Loadshape.storageCurve  npts=24  interval=1 mult=[0 0 -1 -1 -1 -1 -1 0 0 0 0 0 0 0 0 0 0 0 0.8 0.9 0.94 1 0.94 0]

New storage.Battery phases=3 bus1=C kv=0.22 kwrated=15 kwhrated=60 dispmode=follow daily=StorageCurve

Set voltagebases = [0.22]
calcvoltagebases

New monitor.linha1_power element=line.linha1 terminal=1 mode=1 ppolar=no 
New monitor.linha1_voltage element=line.linha1 terminal=1 mode=0
New monitor.carga_power element=load.carga terminal=1 mode=1 ppolar=no
New monitor.carga_voltage element=load.carga terminal=1 mode=0
new monitor.storage_voltage storage.battery terminal=1 ppolar=no mode=0
new monitor.storage_power storage.battery terminal=1 mode=1 ppolar=no
new monitor.storage_general storage.battery terminal=1 mode=3

set mode = daily
set stepsize = 1h
set number = 48
solve
